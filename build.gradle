ext {
    springBootVersion = '1.2.1.RELEASE'
}

apply plugin: 'java'
apply plugin: 'idea'

version = '0.1.0'
group = 'eu.hinsch'

jar {
    baseName = 'spring-boot-extended-logging-properties'
    version = project.version
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    compile("org.springframework.boot:spring-boot-starter:${springBootVersion}")
    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

apply plugin: 'signing'
apply plugin: 'maven'

group = 'eu.hinsch'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'spring-boot-extended-logging-properties'
                packaging 'jar'
                description 'Provide access to spring environment properties in logging configuration'
                url 'https://github.com/lukashinsch/spring-boot-extended-logging-properties/'

                scm {
                    url 'scm:git@github.com:lukashinsch/spring-boot-extended-logging-properties.git'
                    connection 'scm:git@github.com:lukashinsch/spring-boot-extended-logging-properties.git'
                    developerConnection 'scm:git@github.com:lukashinsch/spring-boot-extended-logging-properties.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'lukashinsch'
                        name 'Lukas Hinsch'
                    }
                }
            }
        }
    }
}